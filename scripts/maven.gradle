apply plugin: 'maven'

allprojects {
    ext."signing.keyId" = System.getenv("mavenSigningKeyId")
    ext."signing.secretKeyRingFile" = System.getenv("mavenSigningKeyRingFile")
    ext."signing.password" = System.getenv("mavenSigningKeyPassword")
}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.kuali.maven.wagons:maven-s3-wagon:1.2.1"
}

def target_maven_repo
if (project.hasProperty('target_maven_repo')) {
    target_maven_repo = project.property('target_maven_repo')
}

if (target_maven_repo in ['sonatype', 'sonatype-snapshot']) {
    apply plugin: 'signing'
    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }
}



uploadArchives{
    repositories {
        mavenDeployer {
            if (target_maven_repo == 'sonatype') {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }
                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: System.getenv('sonatypeUsername'), password: System.getenv('sonatypePassword'))
                }
            } else if (target_maven_repo == 'sonatype-snapshot') {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }
                repository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: System.getenv('sonatypeUsername'), password: System.getenv('sonatypePassword'))
                }
            } else if (target_maven_repo == 's3') {
                configuration = configurations.deployerJars
                repository(url: 's3://maven.mparticle.com')
            } else {
                repository(url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath)
            }

            pom.project {
                version project.version
                artifactId project.name
                packaging 'aar'
                name project.ext.kitDescription
                description project.ext.kitDescription
                url 'https://github.com/iterativelyhq/mparticle-android-sdk'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                    }
                }

                scm {
                    url 'https://github.com/iterativelyhq/mparticle-android-sdk'
                    connection 'scm:git:https://github.com/iterativelyhq/mparticle-android-sdk'
                    developerConnection 'scm:git:https://github.com/iterativelyhq/mparticle-android-sdk.git'
                }

                developers {
                    developer {
                        id = 'iteratively-justin'
                        name = 'Justin Fiedler'
                        email = 'justin@iterative.ly'
                    }
                }
            }
        }
    }
}
